#!/usr/bin/env perl

# update ~/.ssh/authorized_keys

# 'packagers' contains a list of ssh keys
# for users allowed to push to repositories

# 'core-group' can push to package repos as well as
# merge the [testing] repository to the rest

use strict;
use warnings;
use Fcntl qw(:flock);

my $HOME=$ENV{HOME};
my $authfile="${HOME}/.ssh/authorized_keys";
my $basefile="${HOME}/.ssh/admin_keys.pub";
my $newfile="${authfile}.new";

my $access="no-port-forwarding,no-X11-forwarding,no-agent-forwarding,no-pty";

sub mydie($@) {
  my ($mesg, @rest) = @_;
  printf STDERR "\033[1;35merror:\033[0;0m ${mesg}\n", @rest;
  exit(1);
}

sub blob_for_file($) {
  my ($file) = @_;

  my $ret = undef;

  open my $cmd, '-|', 'git ls-tree -r master';
  while (<$cmd>) {
    next unless /blob ([a-f0-9]{40})\s+${file}$/;
    $ret = $1;
    last;
  }
  close $cmd;
  return $ret;
}

sub add_packager($$) {
  my ($fh, $line) = @_;

  unless($line =~ /^(\w+):\s*(.*)$/){
    print STDERR "invalid line: $line\n";
    return;
  }

  my $user=$1;
  my $key=$2;

  printf {$fh} "command=\"%s/bin/receive-package %s\",%s %s\n",
    $HOME, $user, $access, $key;
}

sub add_core($$) {
  my ($fh, $key) = @_;
  # to do
  return add_packager($fh, $key);
}

sub buildfile($) {
  my ($fh) = @_;
  my ($in, $blob);
  open $in, '<', $basefile;
  while (<$in>) {
    print {$fh} $_;
  }
  close $in;

  $blob = blob_for_file('packagers');
  if (defined($blob)) {
    open $in, '-|', 'git', 'cat-file', 'blob', $blob;
    while (<$in>) {
      chomp; next if /^(?:#|$)/;
      add_packager($fh, $_);
    }
    close $in;
  }

  $blob = blob_for_file('core-group');
  if (defined($blob)) {
    open $in, '-|', 'git', 'cat-file', 'blob', $blob;
    while (<$in>) {
      chomp; next if /^(?:#|$)/;
      add_core($fh, $_);
    }
    close $in;
  }
}

sub main() {
  open my $fh, '>', $newfile;
  flock($fh, LOCK_EX);
  buildfile($fh);
  rename($newfile, $authfile) or mydie("failed to replace authorization file");
  flock($fh, LOCK_UN);
  close($fh);
}

main();
